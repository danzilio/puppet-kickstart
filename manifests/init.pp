# == Defined Type: kickstart
#
#   This type can be used to generate a kickstart file using the Puppet DSL.
#
# === Parameters:
# [*filename*]
#   Namevar. String. The path to the kickstart file generated by this resource.
# [*commands*]
#   Hash. A hash of commands with arguments as the value. Arguments can be an
#   array. Each argument will be printed on a newline prefaced by the command.
#   For example:
#
#     kickstart { '/tmp/kickstart.ks':
#       commands => {
#         part => [
#           '/boot --ondisk sda --size 128 --fstype ext3',
#           'pv.01 --size 1 --ondisk sda --grow',
#         ]
#       }
#     }
#
#   Would result in:
#
#     part /boot --ondisk sda --size 128 --fstype ext3
#     part pv.01 --size 1 --ondisk sda --grow
#
# [*repos*]
#   Hash. A hash of yum repositories to be used in the kickstart. The key should
#   be the name of the repository while the value should be a hash of arguments
#   to the Kickstart 'repo' command.
#
#   Example:
#
#     kickstart { '/tmp/kickstart.ks':
#       repos => {
#         base => {
#           baseurl => 'http://mirrors.kernel.org/centos/7/os/x86_64/'
#         }
#       }
#     }
#
# [*packages*]
#   Array. An array of package names.
# [*partition_configuration*]
#   Hash. A separate section to define your partition configuration. This
#   follows the same rules as the 'commands' parameter.
# [*fragments*]
#   Hash. An hash of fragments to be evaluated in the template. These are
#   passed as an argument to the `template` function. The hash must have the
#   section name ('pre', 'post') as the key, and an array of paths to ERB
#   templates as the value. Fragments can access the data passed to the
#   $fragment_variables parameter using the instance variable
#   @fragment_variables inside the template.
#
#   Example:
#
#     kickstart { '/tmp/kickstart.ks':
#       fragments => {
#         'post' => [
#           'kickstart_profile/configure_network.erb',
#           'kickstart_profile/install_puppet.erb'
#         ]
#       }
#     }
#
# [*fragment_variables*]
#   Hash. A hash of settings to be consumed by your fragment templates.
# [*addons*]
#   Hash. A separate section to define any addons. This
#   follows the same rules as the 'commands' parameter.
#
#   Example:
#     kickstart { '/var/www/html/kickstart.cfg':
#       addons => {
#         'my_addon_name --arg1 --arg2="value2"' => ['example1','example2','example3'],
#         'my_other_addon' => [],
#       }
#     }
# [*template*]
#   String. A path to an ERB template to use for the kickstart file.
# [*fail_on_unsupported_commands*]
#   Boolean. Raises an error for commands in the $commands hash if set to true,
#   only warns if set to false.
define kickstart (
  $commands,
  $filename                     = $title,
  $repos                        = false,
  $packages                     = false,
  $partition_configuration      = false,
  $fragments                    = false,
  $fragment_variables           = false,
  $addons                       = false,
  $template                     = 'kickstart/kickstart.erb',
  $fail_on_unsupported_commands = true,
) {

  validate_string($template)
  validate_absolute_path($filename)
  validate_bool($fail_on_unsupported_commands)
  validate_hash($commands)

  if $repos { validate_hash($repos) }
  if $packages { validate_array($packages) }
  if $partition_configuration { validate_hash($partition_configuration) }
  if $fragments { validate_hash($fragments) }
  if $fragment_variables { validate_hash($fragment_variables) }
  if $addons { validate_hash($addons) }

  $valid_commands = [
    auth,
    authconfig,
    autopart,
    autostep,
    bootloader,
    btrfs,
    clearpart,
    cmdline,
    device,
    dmraid,
    driverdisk,
    fcoe,
    firewall,
    firstboot,
    group,
    graphical,
    halt,
    ignoredisk,
    install,
    cdrom,
    harddrive,
    liveimg,
    nfs,
    url,
    iscsi,
    iscsiname,
    keyboard,
    lang,
    logvol,
    logging,
    mediacheck,
    monitor,
    multipath,
    network,
    part,
    partition,
    poweroff,
    raid,
    realm,
    reboot,
    repo,
    rescue,
    rootpw,
    selinux,
    services,
    shutdown,
    sshkey,
    sshpw,
    skipx,
    text,
    timezone,
    updates,
    upgrade,
    user,
    vnc,
    volgroup,
    xconfig,
    zerombr,
    zfcp,
    '%include',
    '%ksappend'
  ]

  if $commands and ! empty($commands) {
    $unsupported_commands = difference(keys($commands), $valid_commands)

    if $unsupported_commands and ! empty($unsupported_commands) {
      $ks_error_message = "Unsupported Kickstart commands: ${unsupported_commands}"
      if $fail_on_unsupported_commands { fail $ks_error_message } else { warning $ks_error_message }
    }
  }

  if $partition_configuration and ! empty($partition_configuration) {
    $unsupported_partition_config = difference(keys($partition_configuration), $valid_commands)

    if $unsupported_commands and ! empty($unsupported_partition_config) {
      $part_error_message = "Unsupported Partition commands: ${unsupported_partition_config}"
      if $fail_on_unsupported_commands { fail $part_error_message } else { warning $part_error_message }
    }
  }

  file { $filename:
    ensure  => present,
    content => template($template),
  }

}
